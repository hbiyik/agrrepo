# Maintainer: boogiepop <boogiepop@gmx.com>

_pkgname=iwlwifi-backport
_srcname=backport-iwlwifi
pkgname=$_pkgname-dkms-git
pkgver=core96.r11655
pkgrel=1
pkgdesc="Backport of intel iwlwifi wireless drivers to linux LTS kernels"
arch=('any')
url="https://git.kernel.org/pub/scm/linux/kernel/git/iwlwifi/${_srcname}.git"
license=('GPL')
provides=(${pkgname}=${pkgver} ${_pkgname}-dkms=${pkgver})
source=(git+$url dkms.conf)
sha256sums=('SKIP'
            'SKIP')
depends=('dkms' 'python' 'bc')
makedepends=('linux-aarch64-rockchip-bsp6.1-armbian-git-headers')

_switchcore(){
  cd ${srcdir}/$_srcname
  #_core=$(git ls-remote -b --sort -version:refname | grep -oh core[0-9]* | head -n 1)
  _core=core96
  git checkout release/${_core} --quiet
  git reset --hard --quiet
  printf $_core
}

pkgver() {
  cd ${srcdir}/$_srcname
  _core=$(_switchcore)
  _commits="$(git rev-list --count HEAD)"
  printf $_core.r$_commits
}

build(){
  _core=$(_switchcore)
  cp dkms.conf _dkms.conf
  # Set name and version
  sed -e "s/@PKGBASE@/${_pkgname}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i _dkms.conf
  ln -sf $_srcname $_pkgname-$pkgver
}

check(){
  _core=$(_switchcore)
  for _kernel in ${makedepends[@]}; do
    mkdir -p $srcdir/test_dkms
    rm -rf $srcdir/test_dkms/*
  
    echo "Test Building for $_kernel"
    _kernel_dir=$(pacman -Ql $_kernel | grep -o "/usr/lib/modules/.*/build" | head -1)
    _kernel_ver=$(basename $(pacman -Ql $_kernel | grep -o "/usr/lib/modules/.*/" | head -1))
    dkms build -c _dkms.conf --kernelsourcedir $_kernel_dir --dkmstree=$srcdir/test_dkms -k $_kernel_ver --sourcetree=$srcdir $_pkgname/$pkgver
  done
}

package() {
  install -Dm644 _dkms.conf "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/dkms.conf
  # Copy sources (including Makefile)
  _switchcore
  cp -r ${srcdir}/$_srcname/* "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/
}

