pkgname=python-piper-tts-git
_pkgname=piper1-gpl
pkgver=r130.387ca06
pkgrel=1
pkgdesc="Fast and local neural text-to-speech engine"
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/OHF-Voice/piper1-gpl"
license=('GPL-3.0-or-later')
provides=('piper-tts')
conflicts=('piper-tts')
depends=(glibc python-numpy python-onnxruntime espeak-ng-git)
optdepends=(
	'python-flask: http server'
	'python-pytorch: train'
)
makedepends=(git python-setuptools python-build python-installer python-wheel python-scikit-build cmake ninja)
source=("git+https://github.com/OHF-Voice/piper1-gpl.git"
        "001-system_espeak.patch::https://github.com/OHF-Voice/piper1-gpl/pull/17.patch")
sha256sums=('SKIP' 'SKIP')

pkgver(){
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd ${_pkgname}
    sed -i 's/"cmake", "ninja"//' pyproject.toml
    patch -p1 -N -i ../*.patch
}

build() {
  cd ${_pkgname}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pkgname}
  python -m installer --destdir="$pkgdir" dist/*.whl

  # avoid conflict with GTK application 'piper'
  mv "$pkgdir"/usr/bin/piper "$pkgdir"/usr/bin/piper-tts

  rm "$pkgdir"/usr/COPYING
}

