# Maintainer: Faruk Dikcizgi <boogiepop@gmx.de>
# Contributor: Jan Keith Darunday <jkcdarunday+aur.archlinux.org@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Dan Johansen <strit@manjaro.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Disabled all artifacts except panfrost since they cause color issues on runtime 
#  - Removed Gallium3D drivers/packages for chipsets that don't exist in our ARM devices (intel, VMware svga).
#  - added broadcom and panfrost vulkan packages
#  - enable lto for aarch64

pkgname=mesa-panfrost-git
pkgdesc="Libgl & gbm with panfrost for Mali G610"
pkgver=r185438.b45ed56124f
pkgrel=1
arch=('aarch64' 'armhf')
makedepends=('meson' 'python-mako' 'bison' 'flex' 'cmake'
             'libxfixes' 'libxxf86vm' 'lm_sensors' 'libxshmfence' 'wayland-protocols' 'libdrm' 'libxrandr' 'libglvnd')
depends=('egl-wayland' 'libxfixes' 'libxxf86vm' 'lm_sensors' 'libxshmfence' 'libdrm' 'libxrandr' 'libglvnd' 'vulkan-icd-loader')
provides=('mesa-libgl' 'opengl-driver' 'mesa' ''vulkan-panfrost'')
conflicts=('mesa-libgl' 'mesa')
url="https://gitlab.freedesktop.org/panfrost/mesa.git"
license=('custom')
options=('!lto')
source=(
  "mesa::git+https://gitlab.freedesktop.org/panfrost/mesa.git#branch=panvk-v10-wip"
  LICENSE
 'https://gitlab.freedesktop.org/bbrezillon/mesa/-/commit/ada94e515ca19c0b192dd4fa32b1b214edb1e891.patch'
)
sha512sums=(
  'SKIP'
  'f9f0d0ccf166fe6cb684478b6f1e1ab1f2850431c06aa041738563eb1808a004e52cdec823c103c9e180f03ffc083e95974d291353f0220fe52ae6d4897fecc7'
  'SKIP'
)

prepare() {
  cd mesa
  patch -p1 -i ../ada94e515ca19c0b192dd4fa32b1b214edb1e891.patch
}

pkgver() {
  cd mesa
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  # rm -rf build
  arch-meson mesa build \
    -Dgallium-vdpau=disabled \
    -Dgallium-va=disabled \
    -Dgallium-xa=disabled \
    -Dandroid-libbacktrace=disabled \
    -Dgallium-drivers=panfrost,kmsro \
    -Dvulkan-drivers=panfrost \
    -Ddri3=enabled \
    -Degl=enabled \
    -Dllvm=disabled \
    -Dintel-rt=disabled \
    -Dmicrosoft-clc=disabled \
    -Dintel-clc=auto \
    -Dplatforms=wayland,x11 \
    -Dgallium-nine=false \
    -Dgbm=enabled \
    -Dgles1=disabled \
    -Dgles2=enabled \
    -Dglx=dri \
    -Dshared-glapi=enabled \
    -Dglvnd=true

  ninja -C build
  meson compile -C build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  DESTDIR="${srcdir}/fakeinstall" meson install -C build
}

package() {
  #install libs
  _install fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf
  _install fakeinstall/usr/lib/dri/*_dri.so
  _install fakeinstall/usr/lib/lib{gbm,glapi}.so*
  _install fakeinstall/usr/lib/libGL*
  _install fakeinstall/usr/lib/libEG*
  _install fakeinstall/usr/include
  _install fakeinstall/usr/lib/pkgconfig

  _install fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json
  #_install fakeinstall/usr/lib/d3d
  
  _install fakeinstall/usr/share/vulkan
  _install fakeinstall/usr/lib/libvulkan*
  
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

